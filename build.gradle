buildscript {
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '6.+'
    id 'wtf.gofancy.fancygradle' version '1.1.+'
}

// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'org.spongepowered.mixin'


version = "1.3.0-1.12.2-forge"
group = "com" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "PlanetCore"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

fancyGradle {
    patches {
        resources
        coremods
        asm
    }
}

idea {
    module {
        outputDir = compileJava.destinationDir
        testOutputDir = compileTestJava.destinationDir
        inheritOutputDirs = true
        downloadJavadoc = true
        downloadSources = true
    }
}

minecraft {
    mappings channel: 'stable', version: '39-1.12'
    runs {
        create("client") {
            property("forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP")
            property("forge.logging.console.level", "debug")
        }
        create("server") {
            property("forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP")
            property("forge.logging.console.level", "debug")
        }
    }
}

mixin {
    add sourceSets.main, "mixins.planetcore.refmap"
    config "mixins.planetcore.json"

    debug.verbose = true
}

repositories {
    mavenCentral()
    maven {
        name = "CurseForge Maven" //Base
        url = "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven { url = 'https://repo.spongepowered.org/maven' }
    maven {
        name = 'sonatype-nexus'
        url = 'https://oss.sonatype.org/content/repositories/public/'
    }
    maven { //needed for CubicWorldGen
        name = "JitPack"
        url = "https://jitpack.io/"
    }
}

dependencies {
    minecraft("net.minecraftforge:forge:1.12.2-14.23.5.2860")
    annotationProcessor 'org.spongepowered:mixin:0.8.3:processor'
    implementation 'org.jetbrains:annotations:20.1.0'

    implementation fg.deobf ("com.github.OpenCubicChunks:CubicChunks:c4696fdd5e") {
        transitive = false
    }
    implementation fg.deobf ("com.github.OpenCubicChunks:CubicWorldGen:3d80b2fac5") {
        transitive = false
    }
    implementation fg.deobf("curse.maven:GeckoLib-388172:4020277") {
        transitive = false
    }

}

processResources {
    duplicatesStrategy = 'include'
    // this will ensure that this task is redone when the versions change.
    inputs.property 'version', version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': version
    }

    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar.finalizedBy('reobfJar')
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}
